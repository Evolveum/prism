def verbose = 0
try {
    verbose = Integer.parseInt(params.VERBOSE ?: '0')
} catch (Exception e) { /* ignored */ }

podTemplate(
        activeDeadlineSeconds: 3600,
        idleMinutes: 1,
        // Secret volume with maven settings.xml for deploy, see also sim-link in "build" stage.
        volumes: [secretVolume(secretName: "jenkins-nexus",mountPath: "/root/jenkins-nexus")],
        workspaceVolume: dynamicPVC(requestsSize: "5Gi"),
        containers: [
                containerTemplate(name: 'jnlp',
                        image: 'jenkins/inbound-agent:4.13-2-alpine',
                        runAsUser: '0',
                        resourceLimitCpu: '900m',
                        resourceLimitMemory: '1Gi',
                        resourceRequestCpu: '900m',
                        resourceRequestMemory: '1Gi'),
                containerTemplate(name: 'maven',
                        image: "${params.BUILDER_IMAGE ?: 'maven:3.8.5-openjdk-11-slim'}",
                        ttyEnabled: true,
                        command: 'cat',
                        resourceLimitCpu: '1',
                        // For tests we use -Xmx512m (see the POM file) so 1GB RAM is enough.
                        resourceLimitMemory: '1Gi')
        ]
) {
    node(POD_LABEL) {
        stage("checkout") {
            git branch: "${params.BRANCH ?: 'support-4.5'}",
                    url: 'https://github.com/Evolveum/prism.git'
            echo "git checkout done..."
        }
        stage("build") {
            container('maven') {
                sh """#!/bin/bash
                    # .m2 is mutable and short-term, we just sym-link the settings.xml there.
                    mkdir -p /root/.m2
                    ln -s ../jenkins-nexus/settings.xml /root/.m2/settings.xml

                    # dollar verbose wil be replaced by Groovy, it will be constant string for bash
                    if [ "${verbose}" -ge 1 ]
                    then
                        env | sort
                        mvn --version
                    fi

                    mvn -B -ntp -Dmaven.test.failure.ignore clean deploy -DdeployAtEnd=true

                    if [ "${verbose}" -ge 1 ]
                    then
                        df -h
                    fi
                """

                step([$class: 'Publisher', reportFilenamePattern: '**/testng-results.xml'])
            }
        }
    }
}
