def verbose = params.VERBOSE ?: '0'

podTemplate(
        activeDeadlineSeconds: 600,
        idleMinutes: 1,
        // Secret volume with maven settings.xml for deploy, see also sim-link in "build" stage.
        volumes: [secretVolume(secretName: "jenkins-nexus",mountPath: "/root/jenkins-nexus")],
        workspaceVolume: dynamicPVC(requestsSize: "5Gi"),
        containers: [
                containerTemplate(name: 'jnlp',
                        image: 'jenkins/inbound-agent:4.13-2-alpine',
                        runAsUser: '0',
                        resourceRequestCpu: '1',
                        resourceLimitCpu: '1',
                        resourceRequestMemory: '1Gi',
                        resourceLimitMemory: '1Gi'),
                containerTemplate(name: 'maven',
                        image: params.BUILDER_IMAGE ?: 'maven:3.8.5-openjdk-11',
                        runAsUser: '0',
                        ttyEnabled: true,
                        command: 'cat',
                        resourceRequestCpu: params.BUILDER_CPU ?: '2',
                        resourceLimitCpu: params.BUILDER_CPU ?: '2',
                        // For tests we use -Xmx512m (see the POM file) so 1GB RAM is enough.
                        resourceRequestMemory: '1Gi',
                        resourceLimitMemory: '1Gi')
        ]
) {
    node(POD_LABEL) {
        stage("checkout") {
            git branch: params.BRANCH ?: 'master',
                    url: 'https://github.com/Evolveum/prism.git'
        }
        stage("build") {
            container('maven') {
                sh """#!/bin/bash -ex
                    # .m2 is mutable and short-term, we just sym-link the settings.xml there.
                    mkdir -p /root/.m2
                    ln -s ../jenkins-nexus/settings.xml /root/.m2/settings.xml

                    # dollar verbose wil be replaced by Groovy, it will be constant string for bash
                    if [ "${verbose}" -ge 1 ]
                    then
                        env | sort
                        mvn --version
                        df -h
                    fi

                    mvn -B -ntp -Dmaven.test.failure.ignore clean deploy -DdeployAtEnd=true

                    if [ "${verbose}" -ge 1 ]
                    then
                        df -h
                    fi
                """

                step([$class: 'Publisher', reportFilenamePattern: '**/testng-results.xml'])
            }
        }
    }
}
